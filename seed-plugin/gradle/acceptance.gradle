// Acceptance tests

ext {
    // Parameters
    getParameter = { String key, String defaultValue ->
        if (project.hasProperty(key)) {
            project.properties[key]
        } else {
            defaultValue
        }
    }
    // Default integration test configuration
    jenkinsScheme = getParameter('jenkinsScheme', 'http')
    jenkinsHost = getParameter('jenkinsHost', 'localhost')
    jenkinsPort = getParameter('jenkinsPort', '8080') as int
}

task acceptanceTest(type: Test) {
    doFirst {
        def jenkinsUrl = "${jenkinsScheme}://${jenkinsHost}:${jenkinsPort}"
        println "Docker tests against: ${jenkinsUrl}"
        systemProperty 'jenkinsUrl', jenkinsUrl
    }
    include 'net/nemerosa/seed/acceptance/**'
    include 'net/nemerosa/jenkins/seed/acceptance/**'
}

task integrationTest(type: Test) {
    include 'net/nemerosa/jenkins/seed/integration/**'
    include 'net/nemerosa/seed/integration/**'
}

// Exclude the acceptance tests from the default tests
test {
    exclude 'net/nemerosa/seed/acceptance/**'
    exclude 'net/nemerosa/seed/integration/**'
    exclude 'net/nemerosa/jenkins/seed/acceptance/**'
    exclude 'net/nemerosa/jenkins/seed/integration/**'
}

build.dependsOn integrationTest
integrationTest.mustRunAfter test
