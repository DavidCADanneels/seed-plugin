buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.11.1'
    }
}

apply plugin: 'org.jenkins-ci.jpi'

description = "Seed Jenkins plug-in"

ext {
    // TODO Waiting for JENKINS-27994 to be fixed and a new job-dsl released
    jobDslVersion = '1.32'
}

jenkinsPlugin {
    coreVersion = '1.609'                                            // Version of Jenkins core this plugin depends on.
    displayName = 'Seed Jenkins plug-in'                             // Human-readable name of plugin.
    url = 'http://wiki.jenkins-ci.org/display/JENKINS/Seed-Plugin'   // URL for plugin on Jenkins wiki or elsewhere.
    gitHubUrl = 'https://github.com/jenkinsci/seed-plugin'           // Plugin URL on GitHub. Optional.

    // The developers section is optional, and corresponds to the POM developers section.
    developers {
        developer {
            id 'dcoraboeuf'
            name 'Damien Coraboeuf'
            email 'damien.coraboeuf@gmail.com'
        }
    }
}

// JDK6 compatibility
ext {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

// Joint compilation (Java + Groovy)
sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDir "src/main/groovy" }
    }
    test {
        java { srcDirs = [] }
        groovy { srcDir "src/main/groovy" }
    }
}

dependencies {
    compile 'org.yaml:snakeyaml:1.15'
    // compile "org.codehaus.groovy:groovy-all:2.4.3"

    jenkinsPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.26@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:cloudbees-folder:4.7@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:matrix-project:1.4@jar'
    // Plugins referred to by the Seed builders
    jenkinsPlugins 'org.jenkins-ci.plugins:gradle:1.24@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:description-setter:1.10@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:conditional-buildstep:1.3.3@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:git:2.3.5@jar'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins:subversion:2.5@jar'
    // The Job DSL
    jenkinsPlugins "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    jenkinsTest "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    providedCompile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}@jar"

    testCompile 'org.mockito:mockito-core:1.9.5'
}

// Docker build
apply from: 'gradle/docker.gradle'
apply from: 'gradle/acceptance.gradle'
apply from: 'gradle/local-acceptance.gradle'
