package net.nemerosa.seed.generator

import net.nemerosa.seed.config.SeedProjectEnvironment

class BranchPipelineGeneratorExtension {

    static final String PIPELINE_GENERATOR_PROPERTY_PATH = 'pipeline-generator-property-path'

    private final SeedProjectEnvironment environment
    private final String branch

    BranchPipelineGeneratorExtension(SeedProjectEnvironment environment, String branch) {
        this.environment = environment
        this.branch = branch
    }

    String generate() {

        List<String> snippets = []

        // Gets the property file name
        String propertyPath = environment.getConfigurationValue(PIPELINE_GENERATOR_PROPERTY_PATH, 'seed/seed.properties')

        // TODO Extensions (injection of DSL steps)

        /**
         * Reads information from the property file and generates a Gradle build file
         * for the purpose of download the DSL libraries and extracting the bootstrap
         * script file.
         */
        snippets << """\
configure { node ->
    node / 'builders' / 'net.nemerosa.seed.generator.SeedPipelineGeneratorBuilder' {
        'project' '${environment.id}'
        'projectClass' '${environment.projectClass}'
        'projectScmType' '${environment.scmType}'
        'projectScmUrl' '${environment.scmUrl}'
        'branch' '${branch}'
        'propertyPath' '${propertyPath}'
    }
}
"""

        /**
         * Defines a Gradle step for the build file generated by the previous step:
         * - downloads the dependencies
         * - extract the DSL bootstrap script from the indicated JAR
         */
        snippets << """\
steps {
    conditionalSteps {
        condition {
            stringsMatch('\${SEED_GRADLE}', 'yes', true)
        }
        runner('Fail')
        gradle {
            buildFile 'seed/build.gradle'
            fromRootBuildScriptDir()
            makeExecutable()
            useWrapper()
            tasks 'prepare'
        }
    }
}
"""

        /**
         * Runs the script DSL
         *
         * TODO JENKINS-28171 Add lookupStrategy
         */
        snippets << """\
configure { node ->
    node / 'builders' / 'javaposse.jobdsl.plugin.ExecuteDslScripts' {
        targets 'seed/\${${SeedPipelineGeneratorBuilder.SEED_DSL_SCRIPT_LOCATION}}'
        usingScriptText false
        scriptText ''
        ignoreExisting false
        removedJobAction 'DELETE'
        lookupStrategy 'SEED_JOB'
        additionalClasspath 'seed/lib/*.jar'
    }
}
"""

        // OK
        return snippets.join('\n')
    }
}
